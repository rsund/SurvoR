/ CORRESPONDENSSIANALYYSI  frekvenssi taulukolle, joka on
/ on esitetty survo matriisin muodossa editointikent„ss„
/ k„ynnistys: MATRUN CORRESP <MATRIISI>
/ February 27, 1989, Lauri Tarkkonen
MAT SAVE %1
MAT &P=%1
MAT &C=SUM(&P)
MAT &Pt=&P'
MAT &R=SUM(&Pt)
MAT &Rt=&R'
MAT &Nn=SUM(&Rt)
MAT &E1=&Rt*&C
MAT TRACE &Nn
MAT DIM &P
MAT &G=ZER(row&P+1,col&P+1)
MAT &G(1,1)=&P
MAT &G(row&P+1,1)=&C
MAT &G(row&P+1,0)="sum"
MAT &G(1,col&P+1)=&Rt
MAT &G(0,col&P+1)="sum"
MAT &G(row&P+1,col&P+1)=&Nn
MAT NAME &G AS Observed_frequencies_and_marginals
MAT LOAD &G,######,END+2
MAT &E=(1/tr&Nn)*&E1
MAT &D=&P-&E
MAT &Dc=DV(&C)
MAT &Di=INV(&Dc)
MAT &Dr=DV(&Rt)
MAT TRANSFORM &Dc BY sqrt(X#)
MAT TRANSFORM &Dr BY sqrt(X#)
MAT &Dic=INV(&Dc)
MAT &Dir=INV(&Dr)
MAT &Q1=&Dir*&D
MAT &Q2=&Q1*&Dic
MAT SINGULARVALUE DECOMPOSITION OF  &Q2 TO &U,&L,&V
MAT &X1=&Dir*&U
MAT &Ld=DV(&L)
MAT &X=&X1*&Ld
MAT X=(sqrt(tr&Nn))*&X
MAT &Y1=&Di*&Pt
MAT &Y2=&Y1*X
MAT Lid=INV(&Ld)
MAT Y=&Y2*Lid
MAT &Ll=&L
MAT TRANSFORM  &Ll BY X#*X#
MAT DIM &L
MAT &Res=ZER(row&L,4)
MAT &T=TRI(row&L,row&L)
MAT &Tt=&T'
MAT NAME X AS Row_coordinates
MAT NAME Y AS Column_coordinates
MAT NAME &Ll AS Eigenvalues
MAT &Lls=SUM(&Ll)
MAT TRACE &Lls
MAT &Llp=&Ll
MAT &Lls=SUM(&Ll)
MAT DIM &Ll
MAT &Lpp=CON(row&Ll,col&Ll,tr&Lls)
MAT &Lss=&Tt*&Ll
MAT &Res(1,1)=&Ll
MAT &Res(1,2)=&Lss
MAT TRANSFORM &Llp by &Lpp AND X#/Y#
MAT &Res(1,3)=&Llp
MAT &Llpp=&Tt*&Llp
MAT &Res(1,4)=&Llpp
MAT NAME &Res AS Eigenvalues_Cumulative_Proportions_Cumulative
MAT LOAD &Res,####.###,END+2
MAT NAME &Llp AS Proportions_of_inertia
MAT CLABELS "Proport" TO &Llp
MAT CLABELS "Sum" TO &Lls
MAT CLABELS "Dim" TO X
MAT CLABELS "Dim" TO Y
MAT &C1=(1/tr&Nn)*&C
MAT &R1=(1/tr&Nn)*&R
MAT NAME &C1 AS Column_inertia
MAT NAME &R1 AS Row_inertia
MAT &C1t=&C1'
MAT &R1t=&R1'
MAT LOAD &C1t,###.###,END+2
MAT LOAD &R1t,###.###,END+2
MAT LOAD X,###.###,END+2
MAT LOAD Y,###.###,END+2
MAT &XX=X
MAT TRANSFORM &XX BY X#*X#
MAT &XXS=&XX*&T
MAT &XXt=&XX'
MAT &XXts=SUM(&XXt)
MAT &XXD=DV(&XXts)
MAT &XXDi=INV(&XXD)
MAT &XRes=&XXDi*&XX
MAT &XRes1=&XXDi*&XXS
MAT NAME &XRes AS Quality_and_reproduction_of_rows
MAT NAME &XRes1 AS Cumulative_reproduction_of_rows
MAT LOAD &XRes,###.###,END+2
MAT LOAD &XRes1,###.###,END+2
MAT &YY=Y
MAT TRANSFORM &YY BY X#*X#
MAT &T2=TRI(col&P,col&P)
MAT &YYS=&YY*&T2
MAT &YYt=&YY'
MAT &YYts=SUM(&YYt)
MAT &YYD=DV(&YYts)
MAT &YYDi=INV(&YYD)
MAT &YRes=&YYDi*&YY
MAT &YRes1=&YYDi*&YYS
MAT NAME &YRes AS Quality_and_reproduction_of_columns
MAT NAME &YRes1 AS Cumulative_reproduction_of_columns
MAT LOAD &YRes,###.###,END+2
MAT LOAD &YRes1,###.###,END+2
MAT KILL &*
