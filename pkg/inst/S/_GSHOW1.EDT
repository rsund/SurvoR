SURVO 98 edit field: 101 1000 50 (32 bit version)
0001|*SAVE _GSHOW1 / Sucros for MM slide shows
0003|* /_GSHOW1-HELP / Information about making slide shows
0004|* /ACTIVATE a  / Activate this line before starting the example!
0005|S /_GSHOW1-EX  / Example of a slide show
0007|* Making 'slide shows' in SURVO MM
S   | 7777777777777777777777777777777777
0008|* - Sucro _GSHOW1-HELP
0009|* - Making menu KEYS1.EMF (> < X):
0010|* - Sucro _GSHOW1-START
0011|* - Example: _GSHOW-EX* -files
0012|* - Making a sample of slides
0013|*....................................................................................................
0014|*/_GSHOW1-HELP
0016|* - Sucro _GSHOW1-HELP
0018|*TUTSAVE <Survo>\U\S\_GSHOW1-HELP
0019|*{tempo -1}{init}{W1=_GSHOW1}{call SUR-SAVE}{jump 1,1,1,1}SCRATCH {act}{R}
0020|*{break on}
0021|/
0022|*   {form7} Sucro family _GSHOW1 {R}
0023|*{R}
0024|*These sucros let the user to create 'display shows'{R}
0025|*consisting of sequencies of slides.{R}
0026|*Each slide must be previously created as an EMF file{R}
0027|*by GPLOT schemes with OUTFILE specification.{R}
0028|*{R}
0029|*By activating{R}
0030|*/_GSHOW1-EX{R}
0031|*you can see an example consisting of a menu and two sets{R}
0032|*of slides.{R}
0033|*{R}
0034|*The edit field <Survo>\U\S\_GSHOW1.EDT{R}
0035|*contains listings of _GSHOW1 sucros and information{R}
0036|*for creating own applications.{R}
0037|*Listings of Sucros /_GSHOW-EX* tell how such applications are made.{R}
0038|*{message}        Press ENTER!@{tempo +1}
0039|- on key
0040|-      key _: continue
0041|-   wait 1200
0042|*{message}@{W1=_GSHOW1}{call SUR-RESTORE}{end}
0044|*....................................................................................................
0046|*TUTSAVE <Survo>\U\S\_GSHOW1-SHOW
0047|*{tempo -1}{init}{line start}{erase}{erase}{save systempath W1}
0048|/
0049|*CD {print W1}S{act}{line start}{erase}
0050|/
0051|*LOAD _GSHOW1{act}
0052|/
0053|*{tempo +1}{end}
0055|*....................................................................................................
0057|* - Making menu KEYS1.EMF (> < X):
0059|*TUTSAVE <Survo>\U\S\_GSHOW1-KEYS
0060|/ This sucro displaying the 'control buttons' > < X
0061|/ is called by _GSHOW1-START
0062|/
0063|*{W1=G_SHOW}{call SUR-SAVE}{jump 1,1,1,1}SCRATCH {act}{R}
0065|*S=WX/24 s=S/15{R}
0066|*WHOME=WX-4*S,25 WSIZE=3*S,S MODE=3*S,S{R}
0067|*FRAME=0{R}
0068|*WSTYLE=0    FILL(-1)=0.3,0.3,0.3,0  FILL(-2)=0.1,0.1,0.1,0{R}
0069|*FRAMES=f1,g1,F1,f2,g2,F2,f3,g3,F3{R}
0070|*f1=0,0,S,S,-1 g1=s,s,S-2*s,S-2*s F1=2*s,2*s,S-4*s,S-4*s,-2{R}
0071|*f2=S,0,S,S,-1 g2=S+s,s,S-2*s,S-2*s F2=S+2*s,2*s,S-4*s,S-4*s,-2{R}
0072|*f3=2*S,0,S,S,-1 g3=2*S+s,s,S-2*s,S-2*s F3=2*S+2*s,2*s,S-4*s,S-4*s,2{R}
0073|*{R}
0074|*PEN=[SwissB(25)]{R}
0075|*TEXTS=T1,T2,T3,T4{R}
0076|*T1=>,0.4*S,S/6{R}
0077|*T2=<,1.4*S,S/6{R}
0078|*T3=[WHITE],X,2.4*S,S/6{R}
0079|*OUTFILE=KEYS1{R}
0080|*GPLOT /FRAME / SHOW=0{act}{R}
0081|*{W1=G_SHOW}{call SUR-RESTORE}{end}
0082|*....................................................................................................
0083|* - Sucro _GSHOW1-START
0084|*TUTSAVE  <Survo>\U\S\_GSHOW1-START
0085|/
0086|/ def Wn=W1 Wi=W2 Wj=W3 Wfile=W4 WX=W5 WY=W6 Wxs=W7 Wys=W8 Wgap=W9
0087|/ def Wxhome=W10 Wyhome=W11 Wpath=W12 Wstart=W13
0088|/ def Wt=W14 Wt2=W15
0089|/
0090|*{init}{tempo -1}{save stack}{call *-KEYS}{del stack}{load stack}
0091|/
0092|*{Wn=0}{line start}{ref set 1}
0093|/
0094|+ A: {R}{save word Wfile}
0095|- if Wfile '=' {sp} then goto B
0096|*{Wn=Wn+1}{goto A}
0097|+ B: {R}SCRATCH{erase}{erase}{act}{line start}
0098|/
0099|*SYSTEM SAVE{act}{line start}{erase}
0100|/
0101|/ Size of the screen:
0102|/ WIN SCREEN
0103|/ Current screen: 1024 768
0104|/                 WX   WY
0105|*WIN SCREEN{act}{R}{find :}
0106|/
0107|*{next word}{save word WX}{next word}{save word WY}{R}
0108|/
0109|/ *************** Sizes and places of 'thumbnails':
0110|*{Wxs=WX/8}{Wys=WY/8}
0111|/
0112|*{Wt=WY-80}{Wt=Wt-Wys}{Wt2=Wn-1}{Wgap=Wt/Wt2}
0113|- if Wgap < Wys then goto C
0114|*{Wgap=Wys}
0115|/ *************** Clear setup:
0116|+ C:
0117|*GPLOT /LAYOUT{act}{R}
0118|/
0119|*GPLOT /DEL ALL{act}{tempo +1}{10}{tempo -1}{R}
0120|/
0121|*>DEL MOUSE.MSG{act}{R}
0122|/ *************** Display menu > < X
0123|*.{copy}{R}{R}
0124|*GPLOT FILE KEYS1 / WHOME={Wt=WX-Wxs}{print Wt},25{R}
0125|*WSIZE={print Wxs},30 MODE={print Wxs},30{R}
0126|*WSTYLE=0 MOUSE=MOUSE.MSG{R}
0127|*{u3}{act}{d3}
0128|/
0129|*.{copy}{R}{R}
0130|/ *************** Setting thumbnails:
0131|/  WSIZE={print Wxs},{print Wys} WSTYLE=0 MOUSE=MOUSE.MSG{R}
0132|/
0133|*{Wxhome=WX-Wxs}
0134|/
0135|*{Wi=0}{Wyhome=55}
0136|+ D: {Wi=Wi+1}
0137|- if Wi > Wn then goto G
0138|*{ref set 2}{ref jump 1}{Wj=0}
0139|+ F: {R}{Wj=Wj+1}
0140|- if Wj < Wi then goto F
0141|*{save word Wfile}
0142|/
0143|*{ref jump 2}{tempo +1}{2}{tempo -1}
0144|/
0145|*WSIZE={print Wxs},{print Wys} WSTYLE=0 MOUSE=MOUSE.MSG{R}
0146|*WHOME={print Wxhome},{print Wyhome}{R}
0147|*GPLOT FILE {print Wfile}{act}{R}
0148|*..........{R}
0149|/
0150|*{Wyhome=Wyhome+Wgap}{goto D}
0151|+ G:
0152|*.{copy}{R}{R}
0153|/ *************** Select picture to be shown 'maximized':
0154|*GPLOT /FIX 99{act}{R}
0155|*{save datapath Wpath}{Wi=0}{Wstart=1}
0156|/
0157|+ D2: {tempo +1}{5}{tempo -1}
0158|- if Wstart = 0 then goto D0
0159|/ *************** In the beginning the 1st slide is selected:
0160|*{R}{d}{Wi=1}{Wstart=0}{goto D5}
0161|/
0162|+ D0:
0163|*{line start}{erase}CHECK {print Wpath}MOUSE.MSG{act}{r}{save char Wt}
0164|- if Wt '=' N then goto D2
0165|*{R}LOADP MOUSE.MSG{R}{erase}{u}{act}
0166|/
0167|*{line start}{erase}>DEL MOUSE.MSG{act}{R}
0168|/
0169|*{save word Wt}
0170|- if Wt = 1 then goto D3
0171|*{erase}{Wi=Wt-1}
0172|+ D5: {ref set 2}{ref jump 1}{Wj=0}
0173|+ D4: {R}{Wj=Wj+1}
0174|- if Wj < Wi then goto D4
0175|*{save word Wfile}{next word}{save char Wt}{save word Wt2}{ref jump 2}
0176|/
0177|- if Wt '<>' / then goto D6
0178|*{line start}{ins line}{print Wt2}
0179|/
0180|*{line start} {save word Wt2}{del line}{u}
0181|/
0182|/ {tempo 2}{print Wt2}{100}
0183|/
0184|*{load sucro Wt2}
0185|/
0186|+ D6:
0187|*{line start}{erase}
0188|/
0189|*{erase}WHOME=25,25 WSIZE=0.85*WX,0.85*WY{R}
0190|*{erase}GPLOT FILE {print Wfile}{act}
0191|*{R}{u4}{goto D2}
0192|/ **************** Select from > < X
0193|+ D3:
0194|*{next word}{next word}{save word Wt}{line start}{erase}
0195|*{Wt2=WX/24}
0196|/
0197|- if Wt >= 2*Wt2 then goto E
0198|/
0199|- if Wt < Wt2 then goto N1
0200|- if Wt < 2*Wt2 then goto N2
0201|+ N1: {Wi=Wi+1}
0202|- if Wi <= Wn then goto C2
0203|*{Wi=1}{goto C2}
0204|+ N2: {Wi=Wi-1}
0205|- if Wi > 0 then goto C2
0206|*{Wi=1}
0207|+ C2: {goto D5}
0208|/
0209|+ E: {R}
0210|*{erase}GPLOT /DEL ALL{act}{R}
0211|*{erase}SYSTEM RESTORE{act}{R}
0212|/
0213|*{W1=_GSHOW1}{call SUR-RESTORE}
0214|/
0215|*{tempo +1}{end}
0216|*....................................................................................................
0217|* - Making a sample of slides
0218|*                               TEXTS=T FRAME=0 SHOW=0
0219|*WIN FONT 10,18
0220|*RESIZE 30,90
0222|aGPLOT /LAYOUT SUR_G3.SYS
0223|aGPLOT /FRAME / OUTFILE=S01 T=[BLACK][ArialB(100)],Slide_1,180,120 COLORS=[/WHITE]
0224|aGPLOT /FRAME / OUTFILE=S02 T=[WHITE][ArialB(100)],Slide_2,180,120 COLORS=[/BLACK]
0225|aGPLOT /FRAME / OUTFILE=S03 T=[GREEN][ArialB(100)],Slide_3,180,120 COLORS=[/RED]
0226|aGPLOT /FRAME / OUTFILE=S04 T=[BLACK][ArialB(100)],Slide_4,180,120 COLORS=[/YELLOW]
0227|aGPLOT /FRAME / OUTFILE=S05 T=[YELLOW][ArialB(100)],Slide_5,180,120 COLORS=[/BLUE]
0228|aGPLOT /FRAME / OUTFILE=S06 T=[WHITE][ArialB(100)],Slide_6,180,120 COLORS=[/RED]
0229|aGPLOT /FRAME / OUTFILE=S07 T=[BLUE][ArialB(100)],Slide_7,180,120 COLORS=[/WHITE]
0230|aGPLOT /FRAME / OUTFILE=S08 T=[WHITE][ArialB(100)],Slide_8,180,120 COLORS=[/GREEN]
0231|aGPLOT /FRAME / OUTFILE=S09 T=[CYAN][ArialB(100)],Slide_9,180,120 COLORS=[/YELLOW]
0232|aGPLOT /FRAME / OUTFILE=S10 T=[MAGENTA][ArialB(100)],Slide_10,180,120 COLORS=[/CYAN]
0233|aGPLOT /FRAME / OUTFILE=S11 T=[YELLOW][ArialB(100)],Slide_11,180,120 COLORS=[/MAGENTA]
0234|aGPLOT /FRAME / OUTFILE=S12 T=[WHITE][ArialB(100)],Slide_12,180,120 COLORS=[/BLUE]
0236|*....................................................................................................
0237|*FRAMES=F1,F2  MODE=A,B  A=1024 B=768 s=A/30  SHOW=0
0238|*F1=0,0,A,B,-1 F2=s,s,A-2*s,B-2*s,-2
0239|*FILL(-1)=1.0,1.0,0.0,0 FILL(-2)=0,0,0,0
0241|aGPLOT /FRAME / OUTFILE=S_0
0242|*TEXTS=T T=[ArialB(35)][BLACK],#LINES:CUR+2,CUR+18,0.1*A,0.85*B,35
0243|*This is a schematic example of a 'slide show'
0244|*made by using the  _GSHOW sucro family.
0246|*It includes two sections Slides 1 and Slides 2.
0247|*Both sections will present 6 slides +
0248|*a link back to this 'menu'.
0250|*Items are selected either by clicking
0251|*a corresponding 'thumbnail'
0252|*or sequentically by the > and < buttons
0253|*above the thumbnails.
0255|*The presentation is terminated by clicking the X button.
S   | 111111111111111111111111111111 1111111111111111111111111
0257|*Similar presentations can be easily made
S   | 5
0258|*in Survo of any EMF files
0259|*typically created by GPLOT commands.
0263|*....................................................................................................
0264|*TEXTS=T  FRAMES=F1,F2  MODE=A,B  A=1024 B=768 s=A/30 SHOW=0
0265|*F1=0,0,A,B,-1 F2=s,s,A-2*s,B-2*s,-2
0266|*FILL(-1)=0.3,0.3,0.3,0 FILL(-2)=1,1,0,0
0268|aGPLOT /FRAME / OUTFILE=S_1 T=[WHITE][ArialB(200)],Slides_1,120,400 COLORS=[/BLUE]
0269|aGPLOT /FRAME / OUTFILE=S_2 T=[WHITE][ArialB(200)],Slides_2,120,400 COLORS=[/BLUE]
0270|aGPLOT /FRAME / OUTFILE=S_M T=[WHITE][ArialB(200)],MENU,250,300 COLORS=[/BLUE]
0271|aGPLOT /DEL ALL
0272|*GOTO 1,S / Activate this line!
S   | 555555555555555555555555555555
0273|*....................................................................................................
0274|* - Example: _GSHOW-EX* -files
0276|*TUTSAVE <Survo>\U\S\_GSHOW1-EX
0277|*{tempo -1}{init}{W1=_GSHOW1}{call SUR-SAVE}
0278|/
0279|*{jump 1,1,1,1}SCRATCH {act}{R}
0280|*/_GSHOW1-START{R}
0281|*S_0{R}
0282|*S_1 /_GSHOW1-EX1{R}
0283|*S_2 /_GSHOW1-EX2{R}
0284|*{u4}
0285|*{load _GSHOW1-START}
0286|/
0287|/ {W1=_GSHOW1}{call SUR-RESTORE}  T„m„ x-lopetukseen!
0288|*{end}
0290|*....................................................................................................
0292|*TUTSAVE <Survo>\U\S\_GSHOW1-EX0
0293|*{u}{pre}{d}{pre}{d}{R}
0294|*{R}
0295|*S_0{R}
0296|*S_1 /_GSHOW1-EX1{R}
0297|*S_2 /_GSHOW1-EX2{R}
0298|*{u4}
0299|*{load _GSHOW1-START}
0300|*{end}
0302|*....................................................................................................
0303|*TUTSAVE <Survo>\U\S\_GSHOW1-EX1
0304|*{u}{pre}{d}{pre}{d}{R}
0305|*/_GSHOW1-START{R}
0306|*S01{R}
0307|*S02{R}
0308|*S03{R}
0309|*S04{R}
0310|*S05{R}
0311|*S06{R}
0312|*S_M /_GSHOW1-EX0{R}
0313|*{u8}
0314|*{load _GSHOW1-START}
0315|*{end}
0318|*....................................................................................................
0320|*TUTSAVE <Survo>\U\S\_GSHOW1-EX2
0321|*{u}{pre}{d}{pre}{d}{R}
0322|*/_GSHOW1-START{R}
0323|*S07{R}
0324|*S08{R}
0325|*S09{R}
0326|*S10{R}
0327|*S11{R}
0328|*S12{R}
0329|*S_M /_GSHOW1-EX0{R}
0330|*{u8}
0331|*{load _GSHOW1-START}
0332|*{end}
0333|*....................................................................................................
